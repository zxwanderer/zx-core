    ifndef _PRINT_NUMBER_HEX_ASM_
    define _PRINT_NUMBER_HEX_ASM_

; Из журнала Deja Vu #04, Кемерово, 01.04.98
;  (c)Колотов Сеpгей, г.Шадpинск, SerzhSoft

PHEX_W_str_ptr: dw 00; указатель на строку куда впечатывать символы

; Печать шестнадцатеричного числа хранящегося в HL (0000..FFFF)
; DE - указатель на переменную в которую печатаем символы
PHEX_W:                   
    LD (PHEX_W_str_ptr), DE
    LD A,H                ;печать старшего байта числа
    CALL    PHEX_B        ;вызов процедуры HEX-печати байта
    LD      A,L           ;печать младшего байта числа

PHEX_B:  ;Печать шестнадцатеричного числа в A (00..FF)
    CALL    PHEX_HB       ;печать по полубайтам

;----------------------------------------;
PHEX_HB:                  ;Печать старшего полубайта в A (0..F)
    RRCA                  ;меняем
    RRCA                  ;местами
    RRCA                  ;старший
    RRCA                  ;и младший полубайты

;----------------------------------------;
PHEX_LB:                  ;Печать младшего полубайта в A (0..F)
    PUSH    AF            ;сохранили A на стеке
    AND     #0F           ;отбросили лишние старшие биты
    CP      #0A           ;если A<10,
    JR      C,GO_PHL      ; то перепрыгнули
    ADD     A,"A"-"9"-1   ;корректировка: после "9" идет "A"

GO_PHL:
    ADD     A,"0"         ;перевод в ASCII-код
    PUSH HL
    LD HL, (PHEX_W_str_ptr)
    LD (HL), A
    INC HL
    LD (PHEX_W_str_ptr), HL
    POP HL
    ; RST     #10           ;печать HEX-цифры ("0".."F")
    POP     AF            ;восстановили A со стека
    RET                   ;выход из процедуры

        endif
